// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-20151205.212842-160'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava'
        }
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'org.spongepowered:spongegradle:0.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
ext.minecraftPlugin = plugins.getPlugin('net.minecraftforge.gradle.forge')
ext.DIR_MCP_MAPPINGS = net.minecraftforge.gradle.common.Constants.DIR_MCP_MAPPINGS

// Apply shared implementation Gradle config
apply from: project(':SpongeCommon').file('gradle/implementation.gradle')

// Forge Version
ext.forgeBuild = '1577'

minecraft {
    forgeVersion = forgeBuild
    coreMod = 'org.spongepowered.mod.SpongeCoremod'
}

version = "$minecraft.version-$forgeBuild-$apiVersion-$buildNumber"

compileJava {
    options.compilerArgs += [ "-Atokens=FORGE=$forgeBuild;FML=$forgeBuild" ]
}

jar {
    exclude 'log4j2.xml' // log4j2 configuration is for the development workspace only

    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.launch.Main',
                'TargetForgeBuild': forgeBuild,
                'TargetForgeVersion': minecraft.forgeVersion,
                'FMLCorePlugin': 'org.spongepowered.mod.SpongeCoremod',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0
        )
    }
}

processResources {
    from('src/main/resources') {
        include 'mcmod.info'

        expand(
                version: project.version,
                url: project.url,
                minecraftVersion: minecraft.version,
                forgeVersion: minecraft.forgeVersion
        )
    }
}

shadowJar {
    exclude 'log4j2.xml' // log4j2 configuration is for the development workspace only

    // Temporarily relocate and shade to avoid confict with forge. TODO: Remove when Forge updates to 1.3.0
    relocate('com.typesafe.config', 'configurate.typesafe.config')

    dependencies {
        include dependency('com.typesafe:config')

        // This is not available on the client so we need to shade it
        include dependency('com.google.code.findbugs:jsr305')
    }
}

reobf {
    jar {
        extraFiles 'extraSrg.srg'
    }
}

apply from: 'changelog.gradle'
