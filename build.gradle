// Gradle repositories and dependencies
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-nexus-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven {
            name = 'forge-repo'
            url = 'http://files.minecraftforge.net/maven/'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

// for multiproject fun
evaluationDependsOn('SpongeAPI')

// Default tasks
defaultTasks 'build', 'licenseFormat'

// Apply plugin
apply plugin: 'forge'
apply plugin: 'license'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'com.github.johnrengelman.shadow'

// Basic project information
group = 'org.spongepowered'
archivesBaseName = 'sponge'
// Version -> Minecraft-MinecraftForge-Ours(If any?)
version = '1.8-11.14.0.1239-SNAPSHOT'

// Define variables
ext.buildNumber = project.hasProperty("buildNumber") ? buildNumber : '0'
ext.ciSystem = project.hasProperty("ciSystem") ? ciSystem : 'unknown'
ext.commit = project.hasProperty("commit") ? commit : 'unknown'

// Minimum version of Java required
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories { mavenLocal() }

// MinecraftForge version
minecraft {
    version = "1.8-11.14.0.1246-1.8"
}

configurations {
    deployerJars // maven deployment
}

// Project dependencies
dependencies {
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
    compile 'com.google.inject:guice:4.0-beta5'
    compile project('SpongeAPI')
    testCompile 'junit:junit:4.11'
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.7'
}
shadowJar  {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    dependencies {
        include(dependency('org.slf4j:slf4j-api'))
        include(dependency('org.apache.logging.log4j:log4j-slf4j-impl'))
        include(dependency('com.google.inject:guice'))
    }
    classifier = ''
}
build.dependsOn(shadowJar)

// Filter, process, and include resources
processResources {
    inputs.property "version", version
    inputs.property "mcversion", project.minecraft.version

    // Include in final JAR
    from 'LICENSE.txt'

    // Replace variables
    from('src/main/resources/mcmod.info') {
        expand 'modid': project.name.toLowerCase(),
                'name': project.name,
                'description': project.description,
                'version': version,
                'buildNumber': buildNumber,
                'mcversion': minecraft.version,
                'url': url,
                'authorList': organization
    }
}

// License header formatting
license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    ext.year = project.inceptionYear
    exclude "**/*.info"
    exclude "**/*.json"
    exclude "**/*_at.cfg"
    exclude "assets/**"
    header new File(project("SpongeAPI").getProjectDir(), "HEADER.txt")
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
}

checkstyle {
    configProperties = [
        "name" : project.name,
        "organization" : project.organization,
        "url" : project.url,
        "year" : project.inceptionYear
    ]
    configFile = new File(project("SpongeAPI").getProjectDir(),"checkstyle.xml")
}
tasks.reobf.dependsOn "check"

// Source compiler configuration
tasks.withType(JavaCompile) {
    options.compilerArgs += [ '-Xlint:all', '-Xlint:-path' ]
    options.deprecation = true
    options.encoding = 'utf8'
}

def manifestEntries = [
    "Built-By": System.properties['user.name'],
    "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
    "Implementation-Title": name,
    "Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
    "Implementation-Vendor": url,
    "FMLCorePlugin": "org.spongepowered.mod.SpongeCoremod"
    ]

jar {
    // Jar shading and packaging configuration
    from { project("SpongeAPI").sourceSets.main.output }
    
    // JAR manifest configuration
    manifest.mainAttributes(manifestEntries)
    
    // classifier
    classifier = 'release'
}

task deobfJar(type: Jar) {
    from { project("SpongeAPI").sourceSets.main.output }
    from sourceSets.main.output
}

task sourceJar(type: Jar, dependsOn: sourceMainJava) {
    from "build/sources/java"
    from "build/resources/main/java"
    from project("SpongeAPI").sourceSets.main.java
    from project("SpongeAPI").sourceSets.main.resources
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    from project("SpongeAPI").tasks.javadoc.destinationDir
    classifier = "javadoc"
    dependsOn "SpongeAPI:javadoc"
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    dependsOn reobf
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            if (project.hasProperty("chRepo"))
            {
                repository(url: project.chRepo) {
                    authentication(userName: project.chUsername, password: project.chPassword)
                }
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'Sponge'
                    url 'http://www.spongepowered.org/'

                    scm {
                        url 'https://github.com/SpongePowered/SpongeAPI'
                        connection 'scm:git:git://github.com/SpongePowered/Sponge.git'
                        developerConnection 'scm:git:git@github.com:SpongePowered/Sponge.git'
                    }

                    issueManagement {
                        system 'youtrack'
                        url 'https://issues.spongepowered.org/'
                    }

                    licenses {
                        license {
                            name 'MIT license'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}
