buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava'
        }
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext {
    minecraftPlugin = plugins.getPlugin('net.minecraftforge.gradle.forge')
    forgeBuild = '1515'
    DIR_MCP_MAPPINGS = net.minecraftforge.gradle.common.Constants.DIR_MCP_MAPPINGS
}

minecraft {
    forgeVersion = project.forgeBuild
    coreMod = 'org.spongepowered.mod.SpongeCoremod'
}

apply from: project(':SpongeCommon').file('gradle/implementation.gradle')

// Project dependencies
dependencies {
    compile 'org.apache.ivy:ivy:2.4.0'
}

compileJava {
    options.compilerArgs += [
            "-Atokens=FORGE=$forgeBuild;FML=$forgeBuild"
    ]
}

jar {
    exclude 'log4j2.xml'

    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.launch.Main',
                'TargetForgeBuild': forgeBuild,
                'TargetForgeVersion': minecraft.forgeVersion,
                'FMLCorePlugin': 'org.spongepowered.mod.SpongeCoremod',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0
        )
    }
}

processResources {
    from('src/main/resources') {
        include 'mcmod.info'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'VERSION' : project.version.toString(),
            'URL': project.url.toString(),
            'MCVERSION': minecraft.version.toString()
        ])
    }
}

shadowJar {
    exclude 'log4j2.xml'

    dependencies {
        include dependency('com.google.code.findbugs:jsr305')
        include(dependency('org.apache.ivy:ivy'))
    }
    
    // Exclude a bunch of stuff we don't need from Ivy, so the end jar is smaller
    exclude 'fr/**'
    exclude 'org/apache/ivy/ant/**'
    exclude 'org/apache/ivy/logo.png'
    exclude 'org/apache/ivy/osgi/filter/**'
    exclude 'org/apache/ivy/osgi/obr/**'
    exclude 'org/apache/ivy/osgi/p2/**'
    exclude 'org/apache/ivy/osgi/repo/**'
    exclude 'org/apache/ivy/osgi/updatesite/**'
    exclude 'org/apache/ivy/osgi/util/**'
}

copySrgs << {
    def file = project.file('build/srgs/mcp-srg.srg')
    file.append project.file('extraSrg.srg').text
}
